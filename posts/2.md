---
title: 'O F de "FP"'
author: "Simen Endsjø"
translatedBy: "Pablo Hildo"
date: "02 de dezembro de 2020"
published: false
description: "Arquitetura em Programação Funcional (FP) e Programação Orientada a Objetos (POO) é muito diferente. Enquanto a classe é a principal abstração em OOP, a função é a abstração em FP. Olhando de longe, parece impossível resolver problemas usando o outro paradigma. Para aprendermos como desenvolver programas com funções, nós devemos aprender primeiro como elas funcionam em FP visto que uma função não é o mesmo que um método."
---

### Arquitetura em Programação Funcional (FP) e Programação Orientada a Objetos (POO) é muito diferente. Enquanto a classe é a principal abstração em OOP, a função é a abstração em FP. Olhando de longe, parece impossível resolver problemas usando o outro paradigma. Para aprendermos como desenvolver programas com funções, nós devemos aprender primeiro como elas funcionam em FP visto que uma função não é o mesmo que um método.

Isso não pretende ser uma introdução aprofundada a funções, mas sim mostrar como algumas funcionalidades trabalham juntas para nos permitir construir maiores funcionalidades baseadas em blocos de construção menores.

Eu usarei F# para mostrar os conceitos aqui. Nem todas linguagens funcionam da mesma forma, mas muitas coisas devem ser transferidas facilmente para sua linguagem funcional de escolha. F# é uma linguagem de programação primariamente funcional, multiparadigma, para a plataforma .NET.

Os blocos com coloração de sintaxe nesse post podem ser seguidos imediatamente por um bloco com o resultado não-colorido do exemplo precedente. Isso é o mesmo resultado que você chegaria se você digitasse o bloco no read-eval-print-loop (laço ler-avaliar-mostrar, ou repl) manualmente. Você pode seguir os exemplos usando `fsi.exe` no Windows ou `fsharpi` em sistemas operacionais \*nix.

Um bloco de código fonte como o seguinte:

```fsharp
let i = 1
```

Pode ser imediatamente seguido por um bloco como o seguinte, que mostra o resultado do bloco de código.

```fsharp
val i : int = 1
```

Vamos colocar as mãos na massa com um pequeno exemplo. Nós vamos implementar a seguinte função de pseudo-C# em F#:

```csharp
public static int Add(int x, int y) { return x + y; }
Add(2, 3);
```

```fsharp
let add x y = x + y
add 2 3
```

```
val add : x:int -> y:int -> int
val it : int = 5
```

`let` vai associar um símbolo a um valor ou função. Aqui nós associamos o nome `add` a uma função que recebe dois parâmetros. A sintaxe para funções é bem mais concisa que C#. Parâmetros e argumentos são separados por espaço ao invés de vírgula, e nós não colocamos parênteses ao redor delas. Os tipos também são inferidos em muitos casos para que você não precise especificá-los quando isso não aperfeiçoa o programa. A última expressão é também retornada pela função automaticamente.

O tipo da função é `int -> int -> int`, o que significa que ela recebe dois inteiros e retorna um inteiro (int). Uma função recebendo `int` e `string`, retornando `float`, seria `int -> string -> float`.

Você pode ter visto lambdas (funções sem nome) em várias outras linguagens, e em F# elas são chamadas `fun`.

```fsharp
(fun x y -> x + y) 2 3
```

```
val it : int = 5
```

Nós também podemos associar funções anônimas, dando a elas um nome.

Uma função tem parâmetros. O valor que nós "passamos" para um parâmetro é chamado de argumento. Nós dizemos que aplicamos uma função a argumentos. Para o exemplo acima, `x` e `y` são parâmetros. `2` e `3` são argumentos. Nós aplicamos a função anônima a esses argumentos, associando os parâmetros `x` a `2` e `y` a `3`. Quando o último parâmetro é associado, a função é avaliada.

```fsharp
let add = fun x y -> x + y
add 2 3
```

```
val add : x:int -> y:int -> int
val it : int = 5
```

Note que isso é exatamente o mesmo que nossa primeira versão de `add`! `let add x y = x + y` é só uma forma mais curta de escrever `let add = fun x y -> x + y`.

Mas eu tenho mentido sobre essa função; Não é uma função recebendo dois parâmetros! É uma função recebendo um argumento só, retornando uma outra função que recebe um único argumento!

```fsharp
let add =
    fun x ->
        fun y ->
            x + y
add 2 3
```

```
val add : x:int -> y:int -> int
val it : int = 5
```

Isso também significa que `->` é associativo à direita. O tipo `int -> int -> int` deve ser lido como `(int -> (int -> int))`; recebe um único `int`, retorna uma função que recebe um `int` e retorna um `int`.

Note que o lambda mais interno, `fun y`, referencia a variável `x` que é definida como um parâmetro no corpo do `lambda`. Isso significa que `x` é uma variável livre, que deve ser avaliada pelo ambiente ao seu redor.

Mas... se `add` recebe um único parâmetro, o que acontece se a chamarmos com um único argumento?

```fsharp
let add =
  fun x ->
    fun y ->
      x + y

let add2 = add 2

// alsoAdd2 associa 2 a x, consequentemente se tornando
let alsoAdd2 =
  fun y ->
    2 + y

add2 3
```

```
val add : x:int -> y:int -> int
val add2 : (int -> int)
val alsoAdd2 : y:int -> int
val it : int = 5
```

Isso é chamado de Aplicação Parcial de Função. Nós estamos aplicando a função `add` a `2`, consequentemente associando `x` a `2` no ambiente de `fun y`. No lambda `fun y`, `x` não está mais livre, e é o mesmo que `alsoAdd2`. Nós dizemos que uma função com variáveis livres como `fun y` é uma closure (fecho, fecha sobre seu ambiente).

O processo de traduzir uma função de muitos parâmetros para funções aninhadas recebendo apenas um único parâmetro é chamado de currying. Em linguagens como F# e Haskell, isso é automático; todas funções recebem um único parâmetro, e funções recebendo múltiplos parâmetros são apenas açúcar sintático.

Currying e closures são os principais recursos que precisamos para começar a criar funções mais complexas ao combinar mais funções primitivas.

Closures também podem ser usados para esconder detalhes de implementação ou estado. O exemplo a seguir cria um contador. Você nunca pode modificar a variável diretamente.

```fsharp
let mkCounter () =
  let mutable x = 0
  let get () = x
  let inc () = x <- x + 1
  (get, inc)

let (getX, incX) = mkCounter ()
let (getY, incY) = mkCounter ()

incX ()
incX ()
incX ()

incY ()

(getX (), getY ())
```

```
val mkCounter : unit -> (unit -> int) * (unit -> unit)
val incX : (unit -> unit)
val getX : (unit -> int)
val incY : (unit -> unit)
val getY : (unit -> int)
val it : int * int = (3, 1)
```

Um padrão comum em programação é processar alguns dados em uma pipeline sem se importar diretamente com as etapas intermediárias.

```csharp
X x = FetchSomething();
Y y = SomeProcessing(x);
DoSomething(y);

// E às vezes é escrito em uma única linha para evitar variáveis temporárias
DoSomething(SomeProcessing(FetchSomething()));

// Ou uma função helper (auxiliar) é criada
void LotsOfStuff() {
  X x = FetchSomething();
  Y y = SomeProcessing(x);
  DoSomething(y);
}

// Ou até uma classe pode ser criada para suportar encadeamento de métodos
class C {
  X x;
  Y y;

  C FetchSomething() {
    this.x = ActualFetchSomething();
    return this;
  }

  C SomeProcessing() {
    this.y = ActualSomeProcessing(x);
    return this;
  }

  C DoSomething() {
    ActualDoSomething(y);
    return this;
  }
}

var c = new C();
c.FetchSomething().SomeProcessing().DoSomething();
```

Em programação funcional, nós podemos construir funções maiores ao compor funções menores.

```fsharp
let compose f g x = g (f x)
```

```
val compose : f:('a -> 'b) -> g:('b -> 'c) -> x:'a -> 'c
```

Os tipos estranhos `'a` são argumentos genéricos, e eles serão inferidos das funções que você passa para `compose` (compor). Se nós não omitirmos os parênteses redundantes, fica mais claro que `(a -> b) -> (b -> c) -> (a -> c)`. "Dada uma função de `a` para `b` e uma função de `b` para `c`, crie uma nova função de `a` para `c`".

`compose` vai rodar primeiro a primeira função, e então usar o resultado dela como argumento para a segunda função. E isso pode ser aninhado para que possamos funções complexas quaisquer `compose (compose first next) last`.

```fsharp
let fetchSomething () = 1
let someProcessing x = x + 1
let doSomething y = ()

// Podemos compor elas em múltiplas operações
let fetchThenProcess = compose fetchSomething someProcessing
let fetchThenProcessThenDoSomething = compose fetchThenProcess doSomething

// Ou todas em uma
let lotsOfStuff = compose (compose fetchSomething someProcessing) doSomething
```

```
val fetchSomething : unit -> int
val someProcessing : x:int -> int
val doSomething : y:'a -> unit
val fetchThenProcess : (unit -> int)
val fetchThenProcessThenDoSomething : (unit -> unit)
val lotsOfStuff : (unit -> unit)
```

Ainda que isso funciona bem, tem um pouco de ruído em forma de parênteses e nomes de função. Por sorte, F# tem um "apelido" infixo (no meio, a grosso modo) para `compose`.

```fsharp
(>>)
```

```
val it : (('a -> 'b) -> ('b -> 'c) -> 'a -> 'c)
```

Compor um conjunto de operações passaria a ser `op1 >> op2 >> op3 >> .. >> opN`.

```fsharp
let lotsOfStuff = fetchSomething >> someProcessing >> doSomething
```

```
val lotsOfStuff : (unit -> unit)
```

Outra função útil é `apply` (aplicar). É o tipo de função que parece completamente inútil, mas faz com que "pipelines" sejam bem mais fáceis de ler.

```fsharp
let apply x f = f x
```

```
val apply : x:'a -> f:('a -> 'b) -> 'b
```

Entãaaao... ao invés de escrever `f 1` nós podemos escrever `apply 1 f`! Boa! Mas quando nós consideramos aplicação parcial, `apply 1` vai criar uma função que vai passar `1` como parâmetro para qualquer função. Isso pode ser usado para oferecer parâmetros comuns a funções. `apply connection transaction config` cria uma função que vai enviar esses argumentos para as funções que você "aplicou". `apply` também tem uma versão infixa útil, `|>`, que mostra um caso de uso comum para `apply`.

```fsharp
let f x = x+1
let g x = x.ToString()
let h (x : string) = x.ToLower()

1
|> f
|> g
|> h
```

```
val f : x:int -> int
val g : x:'a -> string
val h : x:string -> string
val it : string = "2"
```

Isso também poderia ser escrito como

```fsharp
(f >> g >> h) 1
```

```
val it : string = "2"
```

Ou até mesmo

```fsharp
1 |> (f >> g >> h)
```

```
val it : string = "2"
```

Isso conclui nossa pequena introdução. Nós falamos um pouco sobre currying, aplicação parcial de funções, argumentos, parâmetros, closures, composição e aplicação. Tem um monte de outras coisas para falar em cada um desses tópicos, e mais um monte sobre funções como um todo, mas isso é o suficiente para começar a explorar como usar funções para abstrações ao invés de classes.
