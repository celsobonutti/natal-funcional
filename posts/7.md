---
title: "Iteração sem for, foreach ou while"
author: "Simen Endsjø"
translatedBy: "Celso Bonutti"
date: "07 de dezembro de 2020"
published: true
description: "Iteração faz parte do feijão com arroz de qualquer linguagem de programação, e esse é o caso de programação funcional tanto quanto programação imperativa, mas como é possível iterar sem `for`, `foreach` ou `while`?"
---

## Iteração faz parte do feijão com arroz de qualquer linguagem de programação, e esse é o caso de programação funcional tanto quanto programação imperativa, mas como é possível iterar sem `for`, `foreach` ou `while`?

Apesar de muitas linguagens funcionais possuírem construtores para loops, esse não é método preferível de iterar. O método funcional usa recursão, mas geralmente utilizamos funções de alto nível como `fold`, que é implementada utilizando recursão.

A forma imperativa, usando uma variável mutável:

```fsharp
let sum xs =
    let mutable tot = 0
    for x in xs do
        tot <- tot + x
    tot
sum [1; 10; 100]
```

```
val sum : xs:seq<int> -> int
val it : int = 111
```

Utilizando recursão:

```fsharp
let sum xs =
    let rec go tot =
        function
        | [] -> tot
        | x::xs -> go (tot + x) xs
    go 0 xs

sum [1; 10; 100]
```

```
val sum : xs:int list -> int
val it : int = 111
```

Utilizando um fold:

```fsharp
let sum xs = Seq.fold (fun s x -> s + x) 0 xs

let a = sum [1; 10; 100]

// Or just
let sum' = Seq.fold (+) 0
let b = sum' [2; 20; 200]

// Scan returns intermediate results
let c = List.scan (+) 0 [1 .. 5]
```

```
val sum : xs:seq<int> -> int
val a : int = 111
val sum' : (int list -> int)
val b : int = 222
val c : int list = [0; 1; 3; 6; 10; 15]
```

```
val sum : xs:seq<int> -> int
val it : int = 111
```

## fold

`fold` - também chamada de `reduce` (como em JavaScript) e `aggregate` em outras linguagens - é uma abstração muito útil, que nós vamos examinar melhor.

```
Seq.fold
```

```
val it : (('a -> 'b -> 'a) -> 'a -> seq<'b> -> 'a) = <fun:clo@166>
```

`'a` é o tipo do nosso estado, e `'b` é o tipo do nosso elemento. Nós precisamos de uma função que receba nosso estado atual e um elemento da sequência, e então retorne o novo estado. É comumente usada para agregar valores como com `sum`, mas contanto que nós cumpremos com os tipos, ela pode fazer praticamente qualquer coisa.

Acho que pode ser útil ver alguns exemplos: aqui implementamos uma média usando uma tupla contendo o número de itens e a soma dos itens.

```fsharp
let avg =
  Seq.fold (fun (c, t) v ->
      (c+1, t+v)
  ) (0, 0)
  >> fun (c, t) -> t/c

avg [50..100]
```

```
val avg : (int list -> int)
val it : int = 75
```

Invertendo uma lista:

```fsharp
let rev = Seq.fold (fun xs x -> x :: xs) []
rev [1 ..5]
```

```
val rev : (int list -> int list)
val it : int list = [5; 4; 3; 2; 1]
```

Pegando o último elemento:

```fsharp
let curry f a b = f (a, b)
let tryLast = Seq.fold (curry (snd >> Some)) None
let a = tryLast []
let b = tryLast [1; 2]
```

```
val curry : f:('a * 'b -> 'c) -> a:'a -> b:'b -> 'c
val tryLast : (int list -> int option)
val a : int option = None
val b : int option = Some 2
```

Aqui nós contamos o número de elementos iguais em uma lista, e retornamos uma nova lista de tuplas contendo o valos do elemento e quantas vezes ele apareceu na lista:

```fsharp
module Option =
    let getOr x = function | Some v -> v | None -> x

let byCount =
  Seq.fold (fun s k ->
    Map.tryFind k s
    |> Option.map ((+) 1)
    |> Option.getOr 1
    |> fun v -> Map.add k v s
  ) Map.empty
  >> Map.toSeq
  >> Seq.sortByDescending snd
  >> Seq.toList

byCount  [1; 2; 1; 1; 1; 2; 3]
```

```
module Option = begin
  val getOr : x:'a -> _arg1:'a option -> 'a
end
val byCount : (int list -> (int * int) list)
val it : (int * int) list = [(1, 4); (2, 2); (3, 1)]
```

Os casos de uso são vários. Quando precisar trabalhar numa lista de elementos, você geralmente pode resolver utilizando um `fold`. Apesar de precisar de um pouco de prática para lê-los, é bom saber que você pode usá-lo. Quando você ficar um mestre em usar folds, ou pelo menos ganhar alguma proficiência com eles, você vai sentir falta quando só puder usar loops.
